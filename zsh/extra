# virtualenvwrapper
#source $HOME/.local/bin/virtualenvwrapper.sh

# tmux
alias tmux="tmux -2"
[ -z "$TMUX"  ] && export TERM=xterm-256color

# C-z fg
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z

# fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# ALT-I - Paste the selected entry from locate output into the command line
fzf-locate-widget() {
  local selected
  if selected=$(locate / | fzf -q "$LBUFFER"); then
    LBUFFER=$selected
  fi
  zle redisplay
}
zle     -N    fzf-locate-widget
bindkey '\ei' fzf-locate-widget

# proxychains
proxychains-command-line() {
    [[ -z $BUFFER ]] && zle up-history
    if [[ $BUFFER == sudo\ proxychains4\ * ]]; then
        LBUFFER="${LBUFFER#sudo\ proxychains4 }"
        LBUFFER="sudo $LBUFFER"
    elif [[ $BUFFER == sudo\ * ]]; then
        LBUFFER="${LBUFFER#sudo }"
        LBUFFER="sudo proxychains4 $LBUFFER"
    elif [[ $BUFFER == proxychains4\ * ]]; then
        LBUFFER="${LBUFFER#proxychains4 }"
    elif [[ $BUFFER == * ]]; then
        LBUFFER="proxychains4 $LBUFFER"
    fi
}
zle     -N    proxychains-command-line
bindkey '^[OQ^[OQ' proxychains-command-line
